import de.gesellix.gradle.docker.tasks.*

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "de.gesellix:gradle-docker-plugin:2018-01-30T22-14-21"
    }
}

plugins {
    id 'java'
    id 'idea'
}

apply plugin: "de.gesellix.docker"

repositories {
    jcenter()
}

dependencies {
    implementation 'org.hamcrest:hamcrest:2.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    testImplementation 'io.rest-assured:rest-assured:3.3.0'

    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

}

task startStubs(type: DockerContainerTask) {

    description 'Start wiremock server container'

    image = "rodolpheche/wiremock"
    containerName = "wiremock-service"
    targetState = DockerContainerTask.State.RELOADED
    ports = ["9999:8080"]
    volumes = ["$rootDir/stubs/wiremock:/home/wiremock"]
}

task stopStubs(type: DockerStopTask) {
    description 'Stop wiremock server container'

    containerId = "wiremock-service"
}
